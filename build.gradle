plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.dddjava.jig-gradle-plugin' version '2021.10.3'
    id 'java'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources.srcDirs = ["src/main/java", "src/main/resources"]
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.0'
    runtimeOnly 'org.postgresql:postgresql'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation('com.github.irof:jig-erd:latest.release')
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude(group: 'org.junit.vintage')
    }
}

test {
    useJUnitPlatform()
}

[compileJava, processResources]*.shouldRunAfter(clean)

task document(type: Copy, dependsOn: jigReports) {
    from 'build/jig'
    into 'src/main/resources/static/document/jig'
}

task documentWithErd(type: Test, dependsOn: document) {
    useJUnitPlatform()
    include '**/EntityRelationshipDiagram**'
}
jigReports.dependsOn(clean, classes)
